package Flex;
import java_cup.runtime.*; 
/*AST Imports*/

import AST.astTreeDeclaration.*;
import AST.mainFunctionDeclaration.*;
import AST.externalDeclaration.*;
import AST.mainFunction.*;
import AST.functionDefinition.*;
import AST.jumpStatement.*;
import AST.iterationStatement.*;
import AST.selectionStatement.*;
import AST.statementList.*;
import AST.declarationList.*;
import AST.compoundStatement.*;
import AST.labeledStatement.*;
import AST.statement.*;
import AST.identifierList.*;
import AST.parameterList.*;
import AST.mainDeclarator.*;
import AST.typeSpecifier.*;
import AST.variableTypeSpecifier.*;
import AST.declaration.*;
import AST.assignmentExpression.*;
import AST.conditionalExpression.*;
import AST.equalityExpression.*;
import AST.relationalExpression.*;
import AST.additiveExpression.*;
import AST.multiplicativeExpression.*;
import AST.basicExpression.*;
import AST.functionCallsExpression.*;
import AST.ioExpressions.*;
import AST.primaryExpression.*;

parser code {:
    public static kcScanner s;
    public static int errors=0;
    public static boolean print=true;
   	public parser(java.io.Reader input) {
        super(s=new kcScanner(input));
    }
    public void report_error(String message, Object info) {
    	if (info instanceof String){
    		errors++; 
    		System.err.println("  "+ errors + "==> " + info + " "+ message + 
    			"\n       Parsing resumed from 2nd token before" + s.current_lexeme()+"\n");
    	}
    	else {
    		StringBuffer m = new StringBuffer("Error ");
    		if (info instanceof java_cup.runtime.Symbol) 
    			m.append( "("+info.toString()+")" );     
    		Symbol temp = (Symbol) info;
    		m.append(" : "+message+ " in "+"line: "+ (temp.left+1) + ", column: "+(temp.right+1));   
    		System.err.println(m);
    	}
    }

    public void report_fatal_error(String message, Object info) {
    	try{
    		report_error(message, info);
    	}catch(Exception e){
    		print=false;
    		System.err.println("Couldn't repair from the error.");
    	}
    }

:};


/* Variables */
terminal INTEGER, BOOLEAN, DOUBLE, CHAR, STRING, VOID;

/* Iterations Structures */
terminal FOR, WHILE;

/* Decision Structures */
terminal IF, ELSE, SWITCH, CASE, DEFAULT, BREAK, CONTINUE;

/* Functions */
terminal MAIN, RETURN;

/* I/O */
terminal PRINT, SCAN;

/* Others */
terminal COMMA, END, COLON;

/* Arithmetic operators */
terminal ADD, MIN, MUL, DIV;

/* Grouping symbols */
terminal OPAR, CPAR, OCBRA, CCBRA;

/* Arithmetic operators */
terminal GREATEREQ, LESSEQ, GREATER, LESS;

/* Logic operators */
terminal OR, AND, NEQU, EQU;

/* Assignment operator */
terminal ASSIGN;

/* Values */
terminal String ID;
terminal Integer INT;
terminal Double DOUBLEVALUE;
terminal Character CHARCONTENT;
terminal String STRINGCONTENT;
terminal Boolean TRUE;
terminal Boolean FALSE;

/* Non terminals */
non terminal astTreeDeclaration ast_tree_declaration;
non terminal mainFunctionDeclaration main_function_declaration;
non terminal externalDeclaration external_declaration;
non terminal mainFunction main_function;
non terminal functionDefinition function_definition;
non terminal jumpStatement jump_statement;
non terminal iterationStatement iteration_statement;
non terminal selectionStatement selection_statement;
non terminal statementList statement_list;
non terminal declarationList declaration_list;
non terminal compoundStatement compound_statement;
non terminal labeledStatement labeled_statement;
non terminal statement statement;
non terminal identifierList identifier_list;
non terminal parameterList parameter_list;
non terminal mainDeclarator main_declarator;
non terminal TS_Type type_specifier;
non terminal VTS_Type variable_type_specifier;
non terminal declaration declaration;
non terminal assignmentExpression assignment_expression;
non terminal conditionalExpression conditional_expression;
non terminal equalityExpression equality_expression;
non terminal relationalExpression relational_expression;
non terminal additiveExpression additive_expression;
non terminal multiplicativeExpression multiplicative_expression;
non terminal basicExpression basic_expression;
non terminal functionCallsExpression function_calls_expression;
non terminal ioExpressions io_expressions;
non terminal primaryExpression primary_expression;


/* Precedences */
precedence left ADD, MIN;
precedence left MUL, DIV;
precedence nonassoc ELSE;

/*AST Declaration*/
ast_tree_declaration ::= main_function_declaration:mFD        										{: /*Imprimir tree*/ :}                                                                      
	;

/*Main Function Declaration*/
main_function_declaration ::= declaration:d main_function_declaration:mFD       					{: RESULT = new MFD_Declaration(d,mFD); :}
    |  main_function:mF external_declaration:eD                             						{: RESULT = new MFD_MainFunction(mF,eD); :}
    ;

/*External Function Declarations*/
external_declaration ::= function_definition:fD external_declaration:eD       						{: RESULT = new ED_ExternalDeclaration(fD,eD); :}
    |                                                                 								{: RESULT = new ED_Lamda(); :} 
	;

/*Main Function*/
main_function ::= type_specifier:tS main_declarator:mD compound_statement:cS          				{: RESULT = new MF_MainFunction(tS,mD,cS);:}  
   ;

/*Function Declaration*/
function_definition ::= type_specifier:tS ID:id OPAR CPAR compound_statement:cS               		{: RESULT = new FD_FunctionDefinition(tS,id,cS);:}
	| type_specifier:tS ID:id OPAR parameter_list:pL CPAR compound_statement:cS 					{: RESULT = new FD_FunctionDefinition(tS,id,pL,cS);:}
;

/*Jump Statements*/
jump_statement ::=   CONTINUE END                                     								{: RESULT = new JS_Continue(); :}
	| BREAK END                                                       								{: RESULT = new JS_Break(); :}
	| RETURN END                                                      								{: RESULT = new JS_Return(); :}
	| RETURN conditional_expression:cE END                                           				{: RESULT = new JS_Return(cE); :}
	;
 
/*Iteration Statements*/
iteration_statement ::=   WHILE OPAR conditional_expression:cE CPAR statement:stm                   {: RESULT = new IT_While(cE,stm); :}
	| FOR OPAR type_specifier:tS ID:id ASSIGN conditional_expression:cE END
      conditional_expression:cE1 END assignment_expression:aE CPAR statement:stm    				{: RESULT = new IT_For(tS,id,cE,cE1,aE,stm); :}
    ;

/*Selection Statements*/
selection_statement ::=   IF OPAR conditional_expression:cE CPAR statement:stm              		{: RESULT = new SS_If(cE,stm); :}      
    | IF OPAR conditional_expression:cE CPAR statement:stm ELSE statement:stm1               		{: RESULT = new SS_IfElse(cE,stm,stm1); :}
    | SWITCH OPAR conditional_expression:cE CPAR OCBRA labeled_statement:lS CCBRA                   {: RESULT = new SS_Switch(cE,lS); :}
	;

/*Statement List*/
statement_list ::=   statement:stm                                           						{: RESULT = new SL_Statement(stm);:}
	| statement_list:sL statement:stm                                       						{: RESULT = new SL_StatementList(sL,stm);:}
	;

/*Declaration Lists*/
declaration_list ::=   declaration:d                                       							{: RESULT = new DL_Declaration(d);:}
	| declaration_list:dL declaration:d                                   	 						{: RESULT = new DL_DeclarationList(dL,d);:}
	;

/*Compound Statements*/
compound_statement ::=   OCBRA CCBRA                                 								{: RESULT = new CS_Empty();:}
	| OCBRA statement_list:sL CCBRA                                     							{: RESULT = new CS_StatementList(sL);:}
	| OCBRA declaration_list:dL CCBRA                                   							{: RESULT = new CS_DeclarationList(dL);:}
	| OCBRA declaration_list:dL statement_list:sL CCBRA												{: RESULT = new CS_Compound(dL,sL);:}
	;

/*Label Statement*/
labeled_statement ::=   CASE conditional_expression:cE COLON statement:stm labeled_statement:lS		{: RESULT = new LS_Case(cE,stm,lS);:}
	| DEFAULT COLON statement:stm                                      								{: RESULT = new LS_Default(stm);:}
	| 																								{: RESULT = new LS_Lamda();:}
	;

/*Statement Generalization*/
statement ::=   compound_statement:cS                                            					{: RESULT = new S_CompoundStatement(cS);:}
	| selection_statement:sS 																		{: RESULT = new S_SelectionStatement(sS);:}                                      										
	| iteration_statement:iS                                           								{: RESULT = new S_IterationStatement(iS);:}
	| jump_statement:jS                                                								{: RESULT = new S_JumpStatement(jS);:}
	;

/*Identifier List*/
identifier_list ::=   primary_expression:id                                         				{: RESULT = new IL_Id(id);:}
	| identifier_list:iL COMMA primary_expression:id                                   				{: RESULT = new IL_identifierList(iL,id);:}
	;

/*Parameter List*/
parameter_list ::=   variable_type_specifier:vTS ID:id                          					{: RESULT = new PL_Id(vTS,id);:}
	| parameter_list:pL COMMA variable_type_specifier:vTS ID:id                    					{: RESULT = new PL_ParameterList(pL,vTS,id);:}
	;

/*Main Declarator*/
main_declarator ::=   MAIN OPAR parameter_list:pL CPAR                 								{: RESULT = new MD_Declaration(pL);:}
    | MAIN OPAR CPAR                                     											{: RESULT = new MD_Declaration();:}
    ;

/*Type specifiers*/
type_specifier ::= CHAR                                                 							{: RESULT = new TS_Type("Character");:}
	| INTEGER                                                       								{: RESULT = new TS_Type("Integer");:}
	| DOUBLE                                                        								{: RESULT = new TS_Type("Double");:}
	| STRING                                                        								{: RESULT = new TS_Type("String");:}
    | BOOLEAN                                                       							    {: RESULT = new TS_Type("Boolean");:}
    | VOID                                                          							    {: RESULT = new TS_Type("Void");:}
	;

/*Variable Type specifiers*/
variable_type_specifier ::= CHAR                                                 					{: RESULT = new VTS_Type("Character");:}
	| INTEGER                                                       								{: RESULT = new VTS_Type("Integer");:}
	| DOUBLE                                                        								{: RESULT = new VTS_Type("Double");:}
	| STRING                                                        								{: RESULT = new VTS_Type("String");:}
    | BOOLEAN                                                       							    {: RESULT = new VTS_Type("Boolean") ;:}
    ;

/*Variable Declarations*/
declaration ::= type_specifier:tS identifier_list:iL END                                          	{: RESULT = new D_identifierList(tS,iL);:}
	| assignment_expression:aE																		{: RESULT = new D_assignmentExpression(aE);:}
	| io_expressions:ioE																			{: RESULT = new D_IOExpressions(ioE);:}
	| function_calls_expression:fCE                                                                 {: RESULT = new D_FunctionCallsExpression(fCE);:}
	;

/*Assignment Expression*/
assignment_expression ::= type_specifier:tS ID:stringValue ASSIGN conditional_expression:cE END		{: RESULT = new AE_Assignation(tS,stringValue,cE);:}
	| ID:stringValue ASSIGN conditional_expression:cE END                                           {: RESULT = new AE_Assignation(stringValue,cE);:}
	;

/*Conditional Expression*/
conditional_expression ::= 	equality_expression:eE													{: RESULT = new CE_equalityExpression(eE);:}
	| conditional_expression:cE AND equality_expression:eE											{: RESULT = new CE_Operation(cE,"&&",eE);:}
	| conditional_expression:cE OR equality_expression:eE											{: RESULT = new CE_Operation(cE,"||",eE);:}
	;

equality_expression ::= relational_expression:rE 													{: RESULT = new EE_relationalExpression(rE);:}
	| equality_expression:eE EQU relational_expression:rE          									{: RESULT = new EE_Operation(eE,"==",rE);:}
	| equality_expression:eE NEQU relational_expression:rE 											{: RESULT = new EE_Operation(eE,"!=",rE);:}
	;

/*Relational Expressions*/
relational_expression ::=   additive_expression:aE                         							{: RESULT = new RE_additiveExpression(aE);:} 
	| relational_expression:rE LESS additive_expression:aE                							{: RESULT = new RE_Operation(rE,"<",aE);:} 
	| relational_expression:rE GREATER additive_expression:aE             							{: RESULT = new RE_Operation(rE,">",aE);:} 
	| relational_expression:rE LESSEQ additive_expression:aE              							{: RESULT = new RE_Operation(rE,"<=",aE);:} 
	| relational_expression:rE GREATEREQ additive_expression:aE           							{: RESULT = new RE_Operation(rE,">=",aE);:} 
	;

/*Additive Expression*/
additive_expression ::=   multiplicative_expression:mE                     							{: RESULT = new AE_multiplicativeExpression(mE);:} 
	| additive_expression:aE ADD multiplicative_expression:mE            							{: RESULT = new AE_Operation(aE,"+",mE);:} 
	| additive_expression:aE MIN multiplicative_expression:mE             							{: RESULT = new AE_Operation(aE,"-",mE);:} 
	;

/*Multiplicative Expression*/
multiplicative_expression ::=   basic_expression:bE                    								{: RESULT = new ME_basicExpression(bE);:} 
	| multiplicative_expression:mE MUL basic_expression:bE                							{: RESULT = new ME_Operation(mE,"*",bE);:} 
	| multiplicative_expression:mE DIV basic_expression:bE                							{: RESULT = new ME_Operation(mE,"/",bE);:}               
	;

/*IO Expressions*/
io_expressions ::= PRINT OPAR STRINGCONTENT:stringContent CPAR END                                  {: RESULT = new IOE_Print(stringContent);:}
    |   PRINT OPAR STRINGCONTENT:stringContent COMMA identifier_list:iL CPAR END             		{: RESULT = new IOE_Print(stringContent,iL);:}
    |   SCAN  OPAR ID:identifierValue CPAR END                                                      {: RESULT = new IOE_Scan(identifierValue);:}
    ;

/*Function calls*/
function_calls_expression ::= ID:id OPAR CPAR END 													{: RESULT = new FCE_FunctionCallsExpression(id);:}
	| ID:id OPAR identifier_list:iL CPAR END 														{: RESULT = new FCE_FunctionCallsExpression(id,iL);:}
	| ID OPAR CPAR error 																			{: parser.report_error("You forgot a ;","End of statement Syntax Error"); :}
	  END
	| ID OPAR identifier_list CPAR error 															{: parser.report_error("You forgot a ;","End of statement Syntax Error"); :}
	  END	  
	;

/*Basic Expressions*/
basic_expression ::=   primary_expression:pE                           								{: RESULT = new BE_primaryExpression(pE);:}
    | function_calls_expression:fCE																	{: RESULT = new BE_FunctionCallsExpression(fCE);:}
    ;

/*Primary Expression*/
primary_expression ::=  ID:identifierValue                              							{: RESULT = new PE_Id(identifierValue); :}
	|   INT:integerContent                                            								{: RESULT = new PE_Content(new Integer(integerContent)); :}
	|   STRINGCONTENT:stringContent                                   								{: RESULT = new PE_Content(new String(stringContent)); :}
	|   DOUBLEVALUE:doubleContent                                     								{: RESULT = new PE_Content(new Double(doubleContent)); :}
	|   CHARCONTENT:charContent                                       								{: RESULT = new PE_Content(new Character(charContent)); :}
	|   TRUE                                            											{: RESULT = new PE_Content(new Boolean(true));:}
	|   FALSE                                       												{: RESULT = new PE_Content(new Boolean(false));:}
	;